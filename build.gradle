plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'io.freefair.lombok' version '6.4.1'
    id 'org.sonarqube' version '3.3'
    id 'application'
    id 'jacoco'
    id 'java'
}

group = 'ru.a_z.tools'
version = '1.0.3'

mainClassName = 'ru.a_z.tools.taxes.Main'
sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.2'
    implementation 'org.apache.logging.log4j:log4j-jcl:2.17.2'
    implementation 'org.dhatim:fastexcel:0.12.14'
    implementation 'technology.tabula:tabula:1.0.5', {
        exclude group: 'commons-logging'
        exclude group: 'com.github.jai-imageio'
        exclude group: 'com.google.code.gson'
        exclude group: 'org.apache.pdfbox', module: 'jbig2-imageio'
        exclude group: 'org.bouncycastle'
        exclude group: 'org.slf4j'
    }

    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'org.dhatim:fastexcel-reader:0.12.14'
    testImplementation 'org.mockito:mockito-core:4.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
}

application {
    run {
        if (project.hasProperty('inputPath') && project.hasProperty('exportType')) {
            args = [project.property('inputPath'), project.property('exportType')]
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    systemProperty 'java.util.logging.config.file', "${buildDir}/resources/test/logging-test.properties"
}

java {
    withSourcesJar()
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "AkaZver_tinkoff-report-to-taxes"
        property "sonar.organization", "aka-zver"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}