plugins {
    id 'java'
    id 'jacoco'
    id 'application'
    id 'org.sonarqube' version '3.2.0'
    id 'io.freefair.lombok' version '5.3.3.3'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group = 'ru.a_z.tools'
version = '1.0.1'

generateLombokConfig.enabled = false
mainClassName = 'ru.a_z.tools.taxes.Main'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'
    implementation 'org.apache.logging.log4j:log4j-jcl:2.14.1'
    implementation 'org.dhatim:fastexcel:0.12.11'
    implementation 'technology.tabula:tabula:1.0.4', {
        exclude group: 'commons-logging'
        exclude group: 'com.github.jai-imageio'
        exclude group: 'com.google.code.gson'
        exclude group: 'org.apache.pdfbox', module: 'jbig2-imageio'
        exclude group: 'org.bouncycastle'
        exclude group: 'org.slf4j'
    }

    testImplementation 'org.assertj:assertj-core:3.20.2'
    testImplementation 'org.dhatim:fastexcel-reader:0.12.11'
    testImplementation 'org.mockito:mockito-core:3.11.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.11.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.2'
}

application {
    run {
        if (project.hasProperty('inputPath') && project.hasProperty('exportType')) {
            args = [project.property('inputPath'), project.property('exportType')]
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    systemProperty 'java.util.logging.config.file', "${buildDir}/resources/test/logging-test.properties"
}

java {
    withSourcesJar()
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "AkaZver_tinkoff-report-to-taxes"
        property "sonar.organization", "aka-zver"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}